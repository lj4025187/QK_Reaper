apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.fighter.reaper"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

task generateReaperPatch(type: Exec) {
    workingDir '../'
    commandLine "java", "-jar" , "./bin/generator.jar", "./bin/reaper.dex", "./bin/reaper.rr"
}

/**
 * publish dex as reaper.dex
 */
task publishReaperDex {
    doLast {
        def dexDir = file('./build/intermediates/transforms/dex/release')
        def targetDexDir = file('../bin/')
        if (!targetDexDir.exists()) {
            targetDexDir.mkdirs()
        }
        dexDir.eachFileRecurse { f ->
            if (f.getName() == 'classes.dex') {
                def targetDexFile = new File(targetDexDir, 'reaper.dex')
                targetDexFile.delete();
                f.withInputStream { targetDexFile << it }
            }
        }
    }
}


afterEvaluate {
    assembleRelease {
        doLast {
            publishReaperDex.execute()
            generateReaperPatch.execute()
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'

    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    compile(name: 'proguard', ext: 'aar')
    // fastjson
    compile 'com.alibaba:fastjson:1.1.52.android'
    // okhttp3
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile files('libs/serversdk-2.0.6.jar')
}
